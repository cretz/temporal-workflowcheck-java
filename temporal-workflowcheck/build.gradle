plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'io.temporal'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

// Add test Java code as test resources
processTestResources {
    from 'src/test/java'
}

dependencies {
    implementation 'org.ow2.asm:asm:9.6'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'io.temporal:temporal-sdk:1.22.3'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

shadowJar {
    relocate 'org.objectweb.asm', 'io.temporal.workflowcheck.shaded.org.objectweb.asm'
}

build.dependsOn shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            // We publish to a local Maven repo for use by the Maven sample
            url "file:///${project.buildDir}/repo"
        }
    }
}